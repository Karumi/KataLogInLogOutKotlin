repositories {
  jcenter()
}

apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-android-extensions"

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.karumi.kataloginlogoutkotlin"
    minSdkVersion 15
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
}

def arrow_version = "0.7.3"

dependencies {
  implementation fileTree(dir: "libs", include: ["*.jar"])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "com.android.support:appcompat-v7:28.0.0-rc02"
  implementation "com.android.support.constraint:constraint-layout:1.1.3"
  implementation "io.arrow-kt:arrow-core:$arrow_version"
  implementation "io.arrow-kt:arrow-syntax:$arrow_version"
  implementation "io.arrow-kt:arrow-typeclasses:$arrow_version"
  implementation "io.arrow-kt:arrow-data:$arrow_version"
  implementation "io.arrow-kt:arrow-instances-core:$arrow_version"
  implementation "io.arrow-kt:arrow-instances-data:$arrow_version"
  kapt "io.arrow-kt:arrow-annotations-processor:$arrow_version"
  implementation "co.metalab.asyncawait:asyncawait:1.0.0"
  implementation "joda-time:joda-time:2.10"
  testImplementation "junit:junit:4.12"
  testImplementation "org.mockito:mockito-core:2.16.0"
  testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
  androidTestImplementation "com.android.support.test:runner:1.0.2"
  androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}

androidExtensions {
  experimental = true
}

configurations {
  ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:0.28.0"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  classpath = configurations.ktlint
  main = "com.github.shyiko.ktlint.Main"
  args "-F", "src/**/*.kt"
}